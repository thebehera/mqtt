plugins {
    id 'kotlin-multiplatform'
}
repositories {
    mavenCentral()
    jcenter()
}

apply from: "../gradle/android.gradle"
apply from: "../gradle/experimental.gradle"
kotlin {
    jvm()
    if (System.getenv("ANDROID_HOME") && JavaVersion.current().ordinal() == JavaVersion.VERSION_1_8.ordinal()) {
        android()
    }
    js {
        nodejs {
            testTask {

            }
        }
    }
    ios("iOS")
    macosX64("macos")
    linuxX64("linux")
    mingwX64("windows")
    
    sourceSets {
        commonMain {
            dependencies {
                implementation project(':buffer')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        if (System.getenv("ANDROID_HOME") && JavaVersion.current().ordinal() == JavaVersion.VERSION_1_8.ordinal()) {
            androidMain {
                dependsOn jvmMain
            }
            androidTest {
                dependsOn jvmTest
            }
        }
        jvmMain {
            dependencies {
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
                implementation npm('tcp-port-used', '1.0.1')
            }
        }

        posixMain {
            dependsOn commonMain
            dependencies {
            }
        }
        posixTest {
            dependencies {
            }
        }

        iOSX64Main {
            dependsOn posixMain
        }
        iOSX64Test {
            dependsOn posixTest
        }
        iOSArm64Main {
            dependsOn posixMain
        }
        iOSArm64Test {
            dependsOn posixTest
        }
        linuxMain {
            dependsOn posixMain
        }
        linuxTest {
            dependsOn posixTest
        }
        macosMain {
            dependsOn posixMain
        }
        macosTest {
            dependsOn posixTest
        }
        windowsMain {
            dependsOn posixMain
        }
        windowsTest {
            dependsOn posixTest
        }
    }

    targets.all {
        compilations.all {
            kotlinOptions {
                freeCompilerArgs += [
                        "-Xuse-experimental=kotlin.ExperimentalUnsignedTypes",
                        "-Xuse-experimental=kotlin.ExperimentalStdlibApi",
                        "-Xopt-in=kotlin.time.ExperimentalTime",
                        "-Xopt-in=kotlin.RequiresOptIn"
                ]
            }
        }
    }
}