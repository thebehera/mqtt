import org.gradle.internal.os.OperatingSystem

plugins {
    id 'kotlin-multiplatform'
    id 'com.squareup.sqldelight'
}
repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'com.squareup.sqldelight'
apply from: "../gradle/android.gradle"
apply from: "../gradle/experimental.gradle"
kotlin {
    jvm()
    if (System.getenv("ANDROID_HOME")) {
        android()
    }
    js {
        browser {
            testTask {
                useKarma {
                    useChromeHeadless()
                    if (System.getenv().containsKey("MOZ_HEADLESS") || System.getenv().containsKey("FIREFOX_BIN")) {
                        useFirefox()
                    }
                }
                enabled = !System.getenv().containsKey("IGNORE_BROWSER_TESTS")
            }
        }
        nodejs {
            testTask {

            }
        }
    }

    ios("iOS")
    macosX64("macos")
    mingwX64("windows")
    sourceSets {
        commonMain {
            dependencies {
                implementation "com.squareup.sqldelight:runtime:$sql_delight_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation project(":buffer")
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        if (System.getenv("ANDROID_HOME")) {
            androidMain {
                dependencies {
                    implementation "com.squareup.sqldelight:android-driver:1.4.3"
                }
            }
            androidTest {
                dependencies {
                    implementation kotlin('test-junit')
                    implementation 'androidx.test.espresso:espresso-core:3.2.0'
                    implementation 'androidx.test:core:1.2.0'
                    implementation 'androidx.test.ext:junit:1.1.1'
                    implementation 'androidx.test:runner:1.2.0'
                    implementation 'androidx.test:rules:1.2.0'
                    implementation 'org.robolectric:robolectric:4.4'
                }
            }
        }
        jvmMain {
            dependencies {
                implementation 'org.xerial:sqlite-jdbc:3.32.3.2'
                implementation "com.squareup.sqldelight:sqlite-driver:1.4.3"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation npm('indexeddbshim', '7.0.0')
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
        nativeTest {
            dependsOn commonTest
        }
        iOSX64Main {
            dependencies {
                implementation "com.squareup.sqldelight:native-driver:1.4.3"
            }
        }
        iOSX64Test {
            dependsOn nativeTest
        }
        iOSArm64Main {
            dependencies {
                implementation "com.squareup.sqldelight:native-driver:1.4.3"
            }
        }
        iOSArm64Test {
            dependsOn nativeTest
        }
        macosMain {
            dependencies {
                implementation "com.squareup.sqldelight:native-driver:1.4.3"
            }
        }
        macosTest {
            dependsOn nativeTest
        }
        windowsMain {
            dependencies {
                implementation "com.squareup.sqldelight:native-driver:1.4.3"
            }
        }
        windowsTest {
            dependsOn nativeTest
        }
    }

    targets.all {
        compilations.all {
            kotlinOptions {
                freeCompilerArgs += [
                        "-Xuse-experimental=kotlin.ExperimentalUnsignedTypes",
                        "-Xuse-experimental=kotlin.ExperimentalStdlibApi",
                        "-Xopt-in=kotlin.RequiresOptIn"
                ]
            }
        }
    }
}


sqldelight {
    Database { // This will be the name of the generated database class.
        packageName = "mqtt.persistence.db"
    }
}