plugins {
    id 'kotlin-multiplatform'
}
repositories {
    mavenCentral()
    jcenter()
}

apply from: "../gradle/android.gradle"
apply from: "../gradle/experimental.gradle"
kotlin {
    jvm()
    if (System.getenv("ANDROID_HOME") && JavaVersion.current().ordinal() == JavaVersion.VERSION_1_8.ordinal()) {
        android()
    }
    ios("iOS")
    js {
        browser {
            testTask {
                useKarma {
                    useChromeHeadless()
                    if (System.getenv().containsKey("MOZ_HEADLESS") || System.getenv().containsKey("FIREFOX_BIN")) {
                        useFirefox()
                    }
                }
                enabled = !System.getenv().containsKey("IGNORE_BROWSER_TESTS")
            }
        }
        nodejs {
            testTask {

            }
        }
    }
    macosX64("macos")
    linuxX64("linux")
    mingwX64("windows")
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation project(":buffer")
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        if (System.getenv("ANDROID_HOME") && JavaVersion.current().ordinal() == JavaVersion.VERSION_1_8.ordinal()) {
            androidMain {
                dependencies {
                }
            }
            androidTest {
                dependencies {
                    implementation kotlin('test-junit')
                }
            }
        }
        jvmMain {
            dependencies {
                implementation 'org.xerial:sqlite-jdbc:3.32.3.2'
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation npm('indexeddbshim', '7.0.0')
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
        nativeMain {
            dependsOn commonMain
            dependencies {
            }
        }
        nativeTest {
            dependencies {
            }
        }
        iOSX64Main {
            dependsOn nativeMain
        }
        iOSX64Test {
            dependsOn nativeTest
        }
        iOSArm64Main {
            dependsOn nativeMain
        }
        iOSArm64Test {
            dependsOn nativeTest
        }
        linuxMain {
            dependsOn nativeMain
        }
        linuxTest {
            dependsOn nativeTest
        }
        macosMain {
            dependsOn nativeMain
        }
        macosTest {
            dependsOn nativeTest
        }
        windowsMain {
            dependsOn nativeMain
        }
        windowsTest {
            dependsOn nativeTest
        }
    }

    targets.all {
        compilations.all {
            kotlinOptions {
                freeCompilerArgs += [
                        "-Xuse-experimental=kotlin.ExperimentalUnsignedTypes",
                        "-Xuse-experimental=kotlin.ExperimentalStdlibApi",
                        "-Xopt-in=kotlin.RequiresOptIn"
                ]
            }
        }
    }
}