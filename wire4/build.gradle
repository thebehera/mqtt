plugins {
    id 'kotlin-multiplatform'
    id 'com.android.library'
}
apply plugin: 'kotlin-android-extensions'
androidExtensions {
    experimental = true
}
apply from: "../gradle/android.gradle"
configurations {
    compileClasspath
}
repositories {
    mavenCentral()
    jcenter()
}
apply from: "../gradle/experimental.gradle"

kotlin {
    jvm {
        compilations.main.kotlinOptions {
            // Setup the Kotlin compiler options for the 'main' compilation:
            jvmTarget = "1.8"
        }
    }
    js {
        browser {
            testTask {
                useKarma {
                    useChromeHeadless()
                }
            }
        }
        nodejs {
            testTask {

            }
        }
    }
    android()
    ios("iOS")
    macosX64("macos")
    linuxX64("linux")
    mingwX64("windows")
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation project(':buffer')
                api project(':wire-core')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk7')
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        androidMain {
            dependencies {
                implementation "androidx.room:room-common:$room_version"
                implementation project(':wire-core')
                implementation kotlin('stdlib-jdk7')
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation(npm("text-encoding", "0.7.0"))
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$coroutines_version"
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }

    targets.all {
        compilations.all {
            kotlinOptions {
                freeCompilerArgs += [
                        "-Xuse-experimental=kotlin.ExperimentalUnsignedTypes",
                        "-Xuse-experimental=kotlin.ExperimentalStdlibApi"
                ]
            }
        }
    }
}
dependencies {
    implementation project(':wire-core')
}