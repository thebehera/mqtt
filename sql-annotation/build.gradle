plugins {
    id 'kotlin-multiplatform' version '1.3.41'
}
repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

apply from: "../gradle/experimental.gradle"
kotlin {
    jvm()
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "org.jetbrains.kotlinx:kotlinx-io-jvm:$kotlinx_io_version"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
    }

    // Given that `-PisLinux=true` command line argument is passed when running on Linux,
    // these targets get published only from a Linux machine.
    // Note that the Kotlin metadata is here, too.
    // The mingwx64() target is automatically skipped as incompatible in Linux builds.
    configure([targets["metadata"], jvm()]) {
        mavenPublication { linuxOnlyPublication ->
            tasks.withType(AbstractPublishToMaven).all {
                onlyIf {
                    publication != linuxOnlyPublication || findProperty("isLinux") == "true"
                }
            }
        }
    }

    targets.all {
        compilations.all {
            kotlinOptions {
                freeCompilerArgs += [
                        "-Xuse-experimental=kotlin.ExperimentalUnsignedTypes"
                ]
            }
        }
    }
}

publishing.publications {
    common(MavenPublication) {
        artifactId = 'mqtt'
        // Add a docs JAR artifact (it should be a custom task):
//                artifact(javadocJar)
    }
}

apply from: file("$rootProject.projectDir/gradle/bintray.gradle")
apply from: file("$rootProject.projectDir/gradle/jacoco.gradle")
