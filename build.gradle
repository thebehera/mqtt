buildscript {
    ext.kotlin_version = '1.3.71'

    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
ext {
    kotlinx_io_version = '0.1.16'
    ktor_version = '1.3.2'
    coroutines_version = '1.3.5'
    room_version = '2.2.2'
}
if (JavaVersion.current().ordinal() != JavaVersion.VERSION_1_8.ordinal()) {
    throw new Exception("*******Java 8 is required!**********\n Please set to java 8 and ensure that it's the default. The current version is set to " + JavaVersion.current().getMajorVersion() + "\n" +
    "Install Java 8 through SDKMan at https://sdkman.io/install")
}
if (!System.getenv("ANDROID_HOME")) {
    throw new Exception("*******Android SDK is required!**********\nis required! Please install the Android SDK from: https://developer.android.com/studio/index.html#command-tools and set your ANDROID_HOME env value")
}
allprojects {
    repositories {
        jcenter()
        mavenLocal()
        maven { url "https://repo1.maven.org/maven2" }
        mavenCentral()
        google()
    }
    // die on warnings
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:all" << "-Werror"
        }
    }

    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        logger.warn("Integration tests are disabled for windows! Reason: Headless chrome isn't working properly")
        tasks.whenTaskAdded {task ->
            if(task.name.contains("jsBrowserTest")) {
                task.enabled = false
            }
        }
    }
}
gradleEnterprise {
    buildScan {
        if (System.getenv("CI")) {
            termsOfServiceUrl = "https://gradle.com/terms-of-service"
            termsOfServiceAgree = "yes"
            publishOnFailure()
            tag "CI"
        }
    }
}