plugins {
    id 'kotlin-multiplatform'
}
repositories {
    mavenCentral()
    jcenter()
}

apply from: "../gradle/android.gradle"
apply from: "../gradle/experimental.gradle"
kotlin {
    jvm()
    android()
    ios("iOS")
    js {
        browser {
            testTask {
                useKarma {
                    useChromeHeadless()
                }
            }
        }
        nodejs {
            testTask {

            }
        }
    }
    macosX64("macos")
    linuxX64("linux")
    mingwX64("windows")
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        androidMain {
            dependsOn jvmMain
        }
        androidTest {
            dependsOn jvmTest
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk7')
                implementation "org.jetbrains.kotlinx:kotlinx-io-jvm:$kotlinx_io_version"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation(npm("text-encoding", "0.7.0"))
                implementation "org.jetbrains.kotlinx:kotlinx-io-js:$kotlinx_io_version"
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
        nativeMain {
            dependsOn commonMain
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-io-native:$kotlinx_io_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutines_version"
            }
        }
        iOSX64Main {
            dependsOn nativeMain
        }
        iOSArm64Main {
            dependsOn nativeMain
        }
        linuxMain {
            dependsOn nativeMain
        }
        macosMain {
            dependsOn nativeMain
        }
        windowsMain {
            dependsOn nativeMain
        }
    }
}