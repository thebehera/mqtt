plugins {
    id 'kotlin-multiplatform'
}
if (System.getenv("ANDROID_HOME")) {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android-extensions'
    androidExtensions {
        experimental = true
    }
    android {
        compileSdkVersion 29
        defaultConfig {
            minSdkVersion 15
        }
        buildTypes {
            release {
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
        packagingOptions {
            exclude 'META-INF/*.kotlin_module'
            exclude 'io/ktor/http/cio/internals/*.kotlin_metadata'
            exclude 'io/ktor/client/features/websocket/*.kotlin_metadata'
            exclude 'io/ktor/http/cio/websocket/*.kotlin_metadata'
            exclude 'io/ktor/http/cio/*.kotlin_metadata'
        }

        compileOptions {
            sourceCompatibility 1.8
            targetCompatibility 1.8
        }
        sourceSets {
            main {
                manifest.srcFile 'src/androidMain/AndroidManifest.xml'
                java.srcDirs = ['src/androidMain/kotlin']
                res.srcDirs = ['src/androidMain/res']
            }
            androidTest {
                java.srcDirs = ['src/androidTest/kotlin']
                res.srcDirs = ['src/androidTest/res']
            }
        }

    }
}
configurations {
    compileClasspath
}
repositories {
    mavenCentral()
    jcenter()
}

apply from: "../gradle/experimental.gradle"
kotlin {
    jvm()
    if (System.getenv("ANDROID_HOME")) {
        android()
    }
    ios("iOS")
    macosX64("macos")
    linuxX64("linux")
    mingwX64("windows")
    js {
        browser {
            testTask {
                useKarma {
                    useChromeHeadless()
                    if (System.getenv().containsKey("MOZ_HEADLESS") || System.getenv().containsKey("FIREFOX_BIN")) {
                        useFirefox()
                    }
                    usePhantomJS()
                }
                enabled = !System.getenv().containsKey("IGNORE_BROWSER_TESTS")
            }
        }
        nodejs {
            testTask {

            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation project(':buffer')
                implementation project(':wire-core')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test')
            }
        }
        jvmMain {
            dependencies {
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        if (System.getenv("ANDROID_HOME")) {
            androidMain {
                dependencies {
                    implementation project(':wire-core')
                }
            }
            androidTest {
                dependencies {
                    implementation kotlin('test')
                    implementation kotlin('test-junit')
                }
            }
        }
        jsMain {
            dependencies {
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }

    targets.all {
        compilations.all {
            kotlinOptions {
                freeCompilerArgs += [
                        "-Xuse-experimental=kotlin.ExperimentalUnsignedTypes",
                        "-Xuse-experimental=kotlin.ExperimentalStdlibApi"
                ]
            }
        }
    }
}
configurations {
    compileClasspath
}