plugins {
    id 'kotlin-multiplatform' version '1.3.41'
}
repositories {
    mavenCentral()
    jcenter()
    maven {
        url  "https://kotlin.bintray.com/ktor"
    }
}
apply from: "../gradle/experimental.gradle"
apply from: "./gradle/integration-tests-mqtt-servers.gradle"

kotlin {
    jvm()

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                compileOnly project(":wire4")
                implementation project(":wire-core")
                implementation project(":time")
                implementation "io.ktor:ktor-client-core:$ktor_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-io:$kotlinx_io_version"
                implementation "io.ktor:ktor-client-websocket:1.2.0-alpha-2"
            }
        }
        commonTest {
            dependencies {
                implementation project(":wire4")
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "io.ktor:ktor-client-cio:$ktor_version"
                implementation "io.ktor:ktor-client-core-jvm:$ktor_version"
                implementation "org.jetbrains.kotlinx:kotlinx-io-jvm:$kotlinx_io_version"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
    }
    targets.all {
        compilations.all {
            kotlinOptions {
                freeCompilerArgs += [
                        "-Xuse-experimental=kotlin.ExperimentalUnsignedTypes"
                ]
            }
        }
    }
}

apply from: file("$rootProject.projectDir/gradle/jacoco.gradle")

jvmTest {
    dependsOn startMosquittoTLS60001
    dependsOn startMosquitto60000Default
    finalizedBy stopMosquittoTLS60001
    finalizedBy stopMosquitto60000Default
}
String osName = System.getProperty("os.name").toLowerCase()
if (osName.contains("linux")) {
    bintrayUpload.dependsOn jvmTest
}
