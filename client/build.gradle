plugins {
    id 'kotlin-multiplatform'
}
repositories {
    mavenCentral()
    jcenter()
}
apply from: "../gradle/experimental.gradle"
apply from: "../gradle/android.gradle"
kotlin {
    jvm()
    android()
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                compileOnly project(":wire4")
                implementation project(":wire-core")
                implementation project(":time")
                implementation "io.ktor:ktor-client-core:$ktor_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-io:$kotlinx_io_version"
                implementation "io.ktor:ktor-client-websocket:1.2.0-alpha-2"
            }
        }
        commonTest {
            dependencies {
                implementation project(":wire4")
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk7')
                implementation "io.ktor:ktor-client-cio:$ktor_version"
                implementation "io.ktor:ktor-client-core-jvm:$ktor_version"
                implementation "org.jetbrains.kotlinx:kotlinx-io-jvm:$kotlinx_io_version"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        androidMain {
            dependsOn(jvmMain)
        }
        androidTest {
            dependsOn(jvmTest)
            dependsOn(jvmMain)
        }
    }
    targets.all {
        compilations.all {
            kotlinOptions {
                freeCompilerArgs += [
                        "-Xuse-experimental=kotlin.ExperimentalUnsignedTypes"
                ]
            }
        }
    }
}
apply from: file("$rootProject.projectDir/gradle/jacoco.gradle")

apply from: "./gradle/integration-tests-mqtt-servers.gradle"

String osName = System.getProperty("os.name").toLowerCase()
if (osName.contains("linux")) {
    bintrayUpload.dependsOn jvmTest
}

tasks.whenTaskAdded { task ->
    if (task.name == "testDebugUnitTest") {
        task.enabled = false
    }
    if(task.name == "testReleaseUnitTest") {
        task.enabled = false
    }
}
